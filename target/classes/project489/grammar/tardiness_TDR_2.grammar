<rule> ::= <Ifs>
<Ifs> ::= (if_ex <cond> <expr> <NestedIfs>)
<NestedIfs> ::= (if_ex <cond> <expr> <expr>)
<cond> ::= <DueDateCond> | <SlackCond> | <ThroughputCond> | <MachineStateCond> | <CriticalRatioCond> | <SystemStateCond>
<DueDateCond> ::= (GT <DueDate> <DueDate>) | (LT <DueDate> <DueDate>) | (Equal <DueDate> <DueDate>)
<SlackCond> ::= (GT <Slack> <Slack>) | (LT <Slack> <Slack>) | (Equal <Slack> <Slack>)
<ThroughputCond> ::= (GT <Throughput> <Throughput>) | (LT <Throughput> <Throughput>) | (Equal <Throughput> <Throughput>) | <UtilCond>
<UtilCond> ::= (GT <Util> <UtilCon>) | (LT <Util> <UtilCon>) | (Equal <Util> <UtilCon>)
<MachineStateCond> ::= (GT <MachineState> <MachineState>) | (LT <MachineState> <MachineState>) | (Equal <MachineState> <MachineState>)
<CriticalRatioCond> ::= (GT <critical_ratio> <critical_ratio>) | (LT <critical_ratio> <critical_ratio>) | (Equal <critical_ratio> <critical_ratio>)
<SystemStateCond> ::= (GT <SystemState> <SystemState>) | (LT <SystemState> <SystemState>) | (Equal <SystemState> <SystemState>)
<DueDate> ::= (SLACK) | <LLD> | <SSD> | (ODD) | (TimeTillDue) | (- <LLD> <RemProcTime>) | (+ <SSD> <RemProcTime>)
<Throughput> ::= (SPT) | (PT) | (NPT) | <RemProcTime> | (OpsLeft) | (TimeInQueue) | (TimeInSystem) | (WINQ)
<SystemState> ::= (TimeInQueue) | (TimeInSystem) | (WINQ)
<MachineState> ::= (PT) | (NPT) | (SPT) | (RemProcTime) | (OpsLeft)
<Slack> ::= (SLACK) | (TimeTillDue) | (- <LLD> <RemProcTime>) | (+ <SSD> <RemProcTime>)
<expr> ::= <nested_expr> | <terminal> | <critical_ratio> | (+ <expr> <DueDateOp>) | (* <expr> <DueDateOp>)
<nested_expr> ::= <uniOp> | <biOp> | <quadOp>
<uniOp> ::= (exp <expr>)
<biOp> ::= (+ <expr> <expr>) | (- <expr> <expr>) | (* <expr> <expr>) | (/ <expr> <expr>) | (min <expr> <expr>) | (max <expr> <expr>)
<quadOp> ::= (if <expr> <expr> <expr>)
<terminal> ::= <constant> | (NPT) | (ODD) | (OpsLeft) | (PT) | <RemProcTime> | (TimeInQueue) | (TimeInSystem) | <LLD> | <SSD> | (WINQ) | (SLACK) | <SPT> | (TimeTillDue)
<DueDateOp> ::= (SLACK) | (- <LLD> <RemProcTime>) | (+ <SSD> <RemProcTime>)
<p> ::= (max <safety_constant> <time_component>)
<critical_ratio> ::= (/ <work_component> <time_component>) | (/ <work_component> <p>)
<work_component> ::= <RemProcTime> | (NPT) | (PT) | (OpsLeft) | (+ <RemProcTime> <TimeInQueue>) | (* <constant> <RemProcTime>)
<time_component> ::= <TimeTillDue> | (SLACK) | <LLD> | <SSD> | (+ <SLACK> <constant>) | (- <LLD> <constant>) | (max <_1> <TimeTillDue>)
<constant> ::= (_0) | (_1) | (_2) | (_5) | (_10) | (_0.2) | (_0.5)
<UtilCon> ::= (_0.95) | (_0.9) | (_0.85) | (_0.8)
<safety_constant> ::= (_1) | (_0.1)
<RemProcTime> ::= (RemProcTime)
<LLD> ::= (LLD)
<SSD> ::= (SSD)
<SPT> ::= (SPT)
<SLACK> ::= (SLACK)
<TimeTillDue> ::= (TimeTillDue)
<TimeInQueue> ::= (TimeInQueue)
<Util> ::= (Util)
<_1> ::= (_1)
